plugins {
    id 'java-library'
    id "io.freefair.lombok" version "6.0.0-m2"
    id 'io.qameta.allure' version '2.11.2'
}

repositories {
    mavenCentral()
}

def restAssuredVersion = "5.3.1",
    allureVersion = "2.21.0"

allure {
    report {
        version.set(allureVersion)
    }
    adapter { // отвечает за появление папочки build/allure-results
        aspectjWeaver.set(true) //обработка аннотации @Step
        frameworks {
            junit5 { //название фреймворка
                adapterVersion.set(allureVersion) //версия интеграции фреймворка и Allure
            }
        }
    }
}

dependencies {
    testImplementation(
            'com.codeborne:selenide:7.5.0',
            'org.junit.jupiter:junit-jupiter:5.9.3',
            'commons-io:commons-io:2.19.0',
            'com.github.javafaker:javafaker:1.0.2',
            'com.codeborne:pdf-test:1.5.0',
            'com.codeborne:xls-test:1.4.3',
            'com.opencsv:opencsv:5.11',
            'com.google.code.gson:gson:2.13.1',
            'com.fasterxml.jackson.core:jackson-core:2.19.0',
            'com.fasterxml.jackson.core:jackson-databind:2.19.0',
            "io.qameta.allure:allure-selenide:$allureVersion",
            'org.assertj:assertj-core:3.27.3',
            'io.rest-assured:rest-assured:5.3.1',
            "io.rest-assured:json-schema-validator:$restAssuredVersion",
            "io.qameta.allure:allure-rest-assured:$allureVersion",
            'org.slf4j:slf4j-simple:2.0.7'
    )
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

tasks.withType(Test) {
    useJUnitPlatform()
    systemProperties(System.getProperties())

    testLogging {
        lifecycle {
            events "started", "skipped", "failed", "standard_error", "standard_out"
            exceptionFormat "short"
        }
    }
}

task demoqa_tests(type: Test) {
    useJUnitPlatform {
        includeTags("Simple_Junit")
        excludeTags("Demoqa_clear_selenide")
    }
}